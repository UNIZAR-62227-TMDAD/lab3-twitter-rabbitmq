import org.springframework.boot.gradle.tasks.run.BootRun

plugins {
    id 'org.springframework.boot' version '2.2.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id 'application'
    id 'com.palantir.docker-run' version '0.25.0'
}

group = 'es.unizar.tmdad'
version = '2020'
sourceCompatibility = 1.8

application {
    mainClassName = 'es.unizar.tmdad.lab3.Application'
}

repositories {
    mavenCentral()
}

ext {
    boostrapVersion = '4.3.1'
    fontawesomeVersion = '5.12.0'
    springSocialVersion = '1.1.0.RELEASE'
    mustacheVersion = '3.1.0'
    stompVersion = '2.3.3'
    sockjsVersion = '1.0.2'
    clhVersion = '1.4.2'
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    implementation("org.springframework.social:spring-social-twitter:${springSocialVersion}")
    implementation('org.springframework.boot:spring-boot-starter-integration')
    implementation('org.springframework.boot:spring-boot-starter-websocket')
    implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-amqp')
    implementation('org.springframework.integration:spring-integration-amqp')

    implementation("com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:${clhVersion}")
    implementation("org.webjars:bootstrap:${boostrapVersion}")
    implementation("org.webjars:font-awesome:${fontawesomeVersion}")
    implementation("org.webjars.npm:mustache:${mustacheVersion}")
    implementation("org.webjars:stomp-websocket:${stompVersion}")
    implementation("org.webjars:sockjs-client:${sockjsVersion}")

    developmentOnly("org.springframework.boot:spring-boot-devtools")

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}

dockerRun {
    name = 'my-rabbit'
    image = 'rabbitmq:3'
    ports('5672:5672')
    daemonize = true
}

task direct(type: BootRun, dependsOn: 'build') {
    group = 'Application'
    description = 'Exchange en modo direct'
    doFirst() {
        main = project.mainClassName
        classpath = sourceSets.main.runtimeClasspath
        systemProperty 'spring.profiles.active', 'direct'
    }
}

task fanout(type: BootRun, dependsOn: 'build') {
    group = 'Application'
    description = 'Exchange en modo fanout'
    doFirst() {
        main = project.mainClassName
        classpath = sourceSets.main.runtimeClasspath
        systemProperty 'spring.profiles.active', 'fanout'
    }
}

task topic(type: BootRun, dependsOn: 'build') {
    group = 'Application'
    description = 'Exchange en modo topic'
    doFirst() {
        main = project.mainClassName
        classpath = sourceSets.main.runtimeClasspath
        systemProperty 'spring.profiles.active', 'topic'
    }
}